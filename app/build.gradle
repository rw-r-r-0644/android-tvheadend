apply plugin: 'com.android.application'

apply from: '../version.gradle'
apply from: '../signing.gradle'
apply from: '../acra.gradle'
apply from: '../play.gradle'

android {
    compileSdkVersion project.ext.compileSdkVersion

    compileOptions {
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "ie.macinnes.tvheadend"
        setProperty("archivesBaseName", applicationId)

        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        versionCode project.ext.versionCode
        versionName project.ext.versionName
    }

    buildTypes {
        debug {
            versionNameSuffix "-SNAPSHOT"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    // Dependency on local binaries
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':htsp')

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.leanback:leanback:1.0.0'
    implementation 'androidx.leanback:leanback-preference:1.0.0'

    implementation 'com.google.android.exoplayer:exoplayer-core:2.11.7'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.11.7'
    // Use our own exoplayer ffmpeg-extension builds
    implementation('org.tvheadend.tvhclient.exoplayer:extension-ffmpeg:2.11.7', { transitive = false })

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}
